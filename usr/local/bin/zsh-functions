#!/bin/zsh

##############################################################################
#
# This is a "library" of zsh(1) functions used by all the Phantom
# Machine Works Z-Shell scripts. All scripts that need any functions
# should "source" this file. It is not intended to be "run" from the
# command line.
#
##############################################################################

# Make mysql calls look a bit more asthetically pleasing. The return
# value of this shell funtion is the exit value of the mysql command
# invocation.
doSQL() {
    local statement="${1:?Need a database query to run.}"
    local useMyCNF=0
    local usableMyCNF=4
    local rdconfig=${RD_CONFIG:-${ROOT:-/}etc/rd.conf}
    local MYSQL=$(whence mysql) ; MYSQL=${MYSQL:-${ROOT:-/}usr/bin/mysql}
    local AWK=$(whence awk) ; AWK=${AWK:-${ROOT:-/}usr/bin/awk}
    local EGREP=$(whence egrep) EGREP=${EGREP:-${ROOT:-/}bin/egrep}

    zmodload zsh/mapfile

    # Rivendell DB Details: Use credentials in ~/.my.cnf if it exists,
    # else get credentials from rd.conf if it exists, else from the
    # environment, else use defaults here.
    # BUG ALERT: this assumes the credentials in .my.cnf are relevant
    # to the Rivendell database.
    if [[ -r ~/.my.cnf ]] ; then
	if [[ $(${EGREP} -c '^(database|host|user|password)' ~/.my.cnf) -ge 4 ]] ; then
	    cd			# Sigh, mapfile cannot deal with path components.
	    myCnfLines=( ${mapfile[.my.cnf]} )

	    # Need to check for each of these parameters in .my.cnf in
	    # order to be able to use it.
	    for parm in database host password user ; do
		if [[ "${myCnfLines}" =~ ${parm} ]] ; then
		    (( useMyCNF++ ))
		fi
	    done
	fi
    else
	# Horribly insecure, but this is the "Rivendell Way".
	if [[ -r ${rdconfig} ]] ; then
	    HOST=$(${AWK} -F'=' '/^Hostname=/{print $2}' ${rdconfig})
	    USER=$(${AWK} -F'=' '/^Loginname=/{print $2}' ${rdconfig})
	    PASSWORD=$(${AWK} -F'=' '/^Password=/{print $2}' ${rdconfig})
	    DATABASE=$(${AWK} -F'=' '/^Database=/{print $2}' ${rdconfig})
	else
	    HOST=${RD_DB_HOST:-"rd-service"}
	    USER=${RD_USER:-"rduser"}
	    PASSWORD=${RD_PASS:-"letmein"}
	    DATABASE=${RD_DATABASE:-"Rivendell"}
	fi
    fi

    if (( useMyCNF == usableMyCNF )) ; then
	${MYSQL} -s -N -e "${statement}"
    else
	${MYSQL} -s -N -B -u ${USER} -p${PASSWORD} -h ${HOST} ${DATABASE} -e "${statement}"
    fi
}
